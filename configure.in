dnl  Process this file with autoconf to produce a configure script.
dnl  Copyright (C) 1998 John Harper <john@dcs.warwick.ac.uk>
dnl  $Id$
dnl
dnl  This file is part of sawmill.
dnl
dnl  sawmill is free software; you can redistribute it and/or modify it
dnl  under the terms of the GNU General Public License as published by
dnl  the Free Software Foundation; either version 2, or (at your option)
dnl  any later version.
dnl
dnl  sawmill is distributed in the hope that it will be useful, but
dnl  WITHOUT ANY WARRANTY; without even the implied warranty of
dnl  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl  GNU General Public License for more details.
dnl
dnl  You should have received a copy of the GNU General Public License
dnl  along with sawmill; see the file COPYING.  If not, write to
dnl  the Free Software Foundation, 675 Mass Ave, Cambridge, MA 02139, USA.

AC_REVISION($Revision$)

AC_INIT(src/sawmill.h)
AC_CONFIG_HEADER(config.h)
AC_CONFIG_AUX_DIR(etc)

dnl Release versioning info
version="0.10"

sawmilldir='${datadir}/sawmill'
lispdir='${sawmilldir}/${version}/lisp'
sawmillexecdir='${libexecdir}/sawmill/${version}/${host_type}'

output_files="src/Makefile lisp/Makefile capplet/Makefile\
 themes/Makefile man/Makefile Makedefs Makefile sawmill.spec"

dnl Find the system type
AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_GCC_TRADITIONAL

dnl Checks for libraries.
AC_CHECK_LIB(nsl, xdr_void)
AC_CHECK_LIB(socket, bind)

dnl Checks for header files.
AC_PATH_XTRA
AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/time.h sys/utsname.h unistd.h memory.h)

dnl Check for librep
AM_PATH_REP(0.5)

dnl is X available?
if test "${no_x}" != "yes"; then
  AC_DEFINE(HAVE_X11)
else
  AC_MSG_ERROR([You need X11 for sawmill])
fi

AM_PATH_IMLIB
AM_PATH_GTK(1.2.0)

build_capplet=0
AC_ARG_ENABLE(capplet,
 [  --enable-capplet	  Build the sawmill GNOME capplet
  --disable-capplet	  Don't built the capplet [default]],
 [if test "$enableval" != "no"; then
    GNOME_CC_CFLAGS="`gnome-config --cflags capplet`"
    GNOME_CC_LIBS="`gnome-config --libs capplet`"
    if test $? -ne 0; then
      AC_MSG_WARN([Can't run gnome-config capplet, not building capplet])
    else
      build_capplet=1
      AC_SUBST(GNOME_CC_CFLAGS)
      AC_SUBST(GNOME_CC_LIBS)
    fi
  fi])
AC_SUBST(build_capplet)

dnl Check for Doug Lea's malloc in libc, otherwise compile dlmalloc.c
doug_lea_malloc=yes
AC_CHECK_FUNC(malloc_get_state, , doug_lea_malloc=no)
AC_CHECK_FUNC(malloc_set_state, , doug_lea_malloc=no)
if test "$doug_lea_malloc" = "no"; then
  LIBOBJS="${LIBOBJS} dlmalloc.o"
else
  AC_DEFINE(LIBC_MALLOC)
fi
AC_DEFINE(DOUG_LEA_MALLOC)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gethostname socket snprintf getpagesize)

dnl Custom tests

dnl If using GCC and it doesn't look as though the cflags have been
dnl set explicitly, add some warning options. Turn off implicit-int
dnl warnings since the X11 includes on Solaris generate a lot of these
if test "x${GCC}" = "xyes" -a "x$CFLAGS" = "x-g -O2"; then
  CFLAGS="${CFLAGS} -Wall -Wmissing-prototypes -Wno-implicit-int"
fi

dnl Nonstandard exported symbols
AC_SUBST(version)
AC_SUBST(sawmilldir)
AC_SUBST(sawmillexecdir)
AC_SUBST(lispdir)
AC_SUBST(HAVE_X11)
AC_SUBST(LIBOBJS)
AC_SUBST(CPPFLAGS)
AC_SUBST(CFLAGS)
AC_SUBST(LIBS)
AC_SUBST(X_CFLAGS)
AC_SUBST(X_LIBS)
AC_SUBST(X_EXTRA_LIBS)
AC_SUBST(IMLIB_CFLAGS)
AC_SUBST(IMLIB_LIBS)

dnl Build all files
AC_OUTPUT(${output_files})

dnl If it doesn't look like GNU Make is being used, give a friendly warning
tem=`make --version -f /dev/null 2>&1 | grep GNU`
if test "x$tem" = "x"; then
  AC_MSG_WARN([You need to use GNU Make when compiling sawmill])
fi

dnl Local variables:
dnl major-mode: sh-mode
dnl End:
